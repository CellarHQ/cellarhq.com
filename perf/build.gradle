apply plugin: 'scala'

def gatlingVersion = '2.0.0-RC5'

repositories {
    jcenter()
}

dependencies {
    compile "org.scala-lang:scala-library:2.10.4"
    testCompile "io.gatling.highcharts:gatling-charts-highcharts:${gatlingVersion}"
}

task gatling << {
    def gatlingClassDirPath = sourceSets.test.output.classesDir.path
    sourceSets.test.output.classesDir.eachFileRecurse { file ->
        if (file.path.endsWith('.class')) {
            def scenarioClass = (file.path - (gatlingClassDirPath + File.separator) - '.class').replace(File.separator, '.')
            if (scenarioClass.endsWith('Simulation')) {
                javaexec {
                    main = 'io.gatling.app.Gatling'
                    classpath = sourceSets.test.output + sourceSets.test.runtimeClasspath
                    args '-rf', "${project.buildDir.absolutePath}/gatling-reports", '-s', scenarioClass
                }
            }
        }
    }
}
gatling.dependsOn 'compileTestScala'
