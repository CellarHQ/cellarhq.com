buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id 'java-library'
  id 'groovy'
  id 'idea'
  id "org.liquibase.gradle" version "2.0.2"
  id "nu.studer.jooq" version "4.1"
}

repositories {
  jcenter()
}

dependencies {
  jooqRuntime databaseDriver
  implementation 'org.codehaus.groovy:groovy-all:2.4.6'
  api databaseDriver
  api 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
  api "org.jooq:jooq:${jooqVersion}"
  api "org.jooq:jooq-meta:${jooqVersion}"
  implementation "org.jooq:jooq-codegen:${jooqVersion}"
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

liquibase {
  activities {
    development {
      changeLogFile "${rootDir}/model/migrations/migrations.xml"
      url 'jdbc:postgresql://localhost:15432/cellarhq'
      username 'cellarhq'
      password 'cellarhq'
    }

    testing {
      changeLogFile "${rootDir}/model/migrations/migrations.xml"
      url 'jdbc:postgresql://localhost:15432/cellarhq_testing'
      username 'cellarhq'
      password 'cellarhq'
    }

    production {
      if (System.getenv("DATABASE_URL")) {
        URI dbUri = new URI(System.getenv("DATABASE_URL"));

        String herokuUsername = dbUri.getUserInfo().split(":")[0];
        String herokuPassword = dbUri.getUserInfo().split(":")[1];
        String herokuJdbc = "jdbc:postgresql://" + dbUri.getHost() + ':' + dbUri.getPort() + dbUri.getPath();

        changeLogFile "${rootDir}/model/migrations/migrations.xml"
        url herokuJdbc
        username herokuUsername
        password herokuPassword
      }
    }
  }

  if (project.hasProperty('runList')) {
    runList = project.property('runList')
  } else {
    if (rootProject.gradle.startParameter.taskNames.contains('test')
      || rootProject.gradle.startParameter.taskNames.contains('check')) {
      runList = 'testing'
    } else {
      runList = 'development'
    }
  }
}

jooq {
  cellarhq(sourceSets.main) {
    jdbc {
      driver = 'org.postgresql.Driver'
      url = 'jdbc:postgresql://localhost:15432/cellarhq'
      user = 'cellarhq'
      password = 'cellarhq'
      schema = 'public'
    }
    generator {
      name = 'org.jooq.codegen.DefaultGenerator'
      strategy {
        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
      }
      database {
        name = ' org.jooq.meta.postgres.PostgresDatabase'
        inputSchema = 'public'
      }
      generate {
        pojos = true
        jpaAnnotations = true
        daos = true
      }
      target {
        directory = "${this.project.projectDir}/src/main/generated"
        packageName = 'com.cellarhq.generated'
      }
    }
  }
}
