buildscript {
    repositories {
        jcenter()
        maven { url "http://oss.jfrog.org/repo" }
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.8-SNAPSHOT"
        classpath 'net.saliman:gradle-liquibase-plugin:1.0.0'
        classpath databaseDriver
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
        classpath 'se.transmode.gradle:gradle-docker:1.2'

    }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'liquibase'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'docker'
apply from: "${rootDir}/gradle/codenarc.gradle"

group = 'com.cellarhq'


repositories {
    jcenter()
    maven { url "http://repo.springsource.org/repo" } // for springloaded
    maven { url "http://oss.jfrog.org/repo" }
}

dependencies {
    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded "org.springsource.loaded:springloaded:1.1.5.RELEASE"

    compile ratpack.dependency('codahale-metrics')
    compile ratpack.dependency('h2')
    compile ratpack.dependency('hikari')
    compile ratpack.dependency('pac4j')
    compile ratpack.dependency('remote')
    compile 'org.pac4j:pac4j-http:1.5.1'
    compile 'org.pac4j:pac4j-oauth:1.5.1'

    compile databaseDriver
    compile 'org.hibernate:hibernate-core:4.3.5.Final'
    compile 'com.github.marschall:threeten-jpa:1.0.1'

    compile 'org.mindrot:jbcrypt:0.3m'
    compile 'com.github.slugify:slugify:2.1.2'

    compile 'org.hibernate:hibernate-validator:5.1.2.Final'
    compile 'javax.el:javax.el-api:2.2.4'
    compile 'org.glassfish.web:javax.el:2.2.4'

    // Default SLF4J binding.  Note that this is a blocking implementation.
    // See here for a non blocking appender http://logging.apache.org/log4j/2.x/manual/async.html
    runtime 'org.slf4j:slf4j-simple:1.7.7'

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2'
    testCompile "org.gebish:geb-spock:0.9.2"
    // Required for mocking multi arg constructor
    testRuntime "org.objenesis:objenesis:1.2"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.40.0"
    testCompile ratpack.dependency("remote-test")

}

liquibase {
    activities {
        development {
            changeLogFile 'migrations/migrations.xml'
            url 'jdbc:postgresql://localhost:15432/cellarhq'
            username 'cellarhq'
            password 'cellarhq'
        }
//        production {}
    }
}

task cellarHqDocker(type: Docker) {
    dependsOn shadowJar
    applicationName = 'cellarhq'
    tagVersion = '0.1'
    dockerfile = project.file('Dockerfile')

    doFirst {
        addFile project.shadowJar.outputs.files.singleFile
    }
}

build.dependsOn cellarHqDocker
