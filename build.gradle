buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.7"
        classpath 'net.saliman:gradle-liquibase-plugin:1.0.0'
        classpath databaseDriver
    }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: 'liquibase'
apply from: "${rootDir}/gradle/codenarc.gradle"
apply from: "${rootDir}/gradle/findbugs.gradle"

repositories {
    jcenter()
    maven { url "http://repo.springsource.org/repo" } // for springloaded
}

dependencies {
    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded "org.springsource.loaded:springloaded:1.1.5.RELEASE"

    compile ratpack.dependency('codahale-metrics')
    compile ratpack.dependency('h2')
    compile ratpack.dependency('hikari')
    compile ratpack.dependency('pac4j')
    compile 'org.pac4j:pac4j-http:1.5.1'
    compile ratpack.dependency('rx')

    compile databaseDriver
    compile 'org.hibernate:hibernate-core:4.3.5.Final'
    compile 'org.liquibase:liquibase-core:3.2.2'

    // Default SLF4J binding.  Note that this is a blocking implementation.
    // See here for a non blocking appender http://logging.apache.org/log4j/2.x/manual/async.html
    runtime 'org.slf4j:slf4j-simple:1.7.7'

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

liquibase {
    activities {
        development {
            changeLogFile 'migrations/migrations.xml'
            url 'jdbc:postgresql://localhost:15432/cellarhq'
            username 'cellarhq'
            password 'cellarhq'
        }
//        production {}
    }
}